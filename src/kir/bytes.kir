let max_integer_u8:  u8  = 0xFF;
let max_integer_u16: u16 = 0xFFFF;
let max_integer_u32: u32 = 0xFFFFFFFF;
let max_integer_u64: u64 = 0xFFFFFFFFFFFFFFFF;

let max_integer_s8:  s8  = 0x7F;
let max_integer_s16: s16 = 0x7FFF;
let max_integer_s32: s32 = 0x7FFFFFFF;
let max_integer_s64: s64 = 0x7FFFFFFFFFFFFFFF;

let min_integer_s64: s64 = cast(s64, 0x8000000000000000);

fun min_uint(a: uint, b: uint) => uint {
    if a < b {
        ret a;
    }
    ret b;
}

fun max_uint(a: uint, b: uint) => uint {
    if a < b {
        ret b;
    }
    ret a;
}

fun align_uint(x: uint, a: uint) => uint {
	let mask: uint = a - 1;
	let delta: uint = (^x + 1) & mask;
	ret x + delta;
}

// Basic assert function.
fun must(c: bool) {
	if c {
		ret;
	}

	panic_trap();
}

// Get unsigned 16-bit integer from first 2 bytes of a given bytes chunk.
// Function assumes little endian order of bytes inside the chunk.
fun unsafe_get_u16le(p: [*]u8) => u16 {
	ret (cast(u16, p.[0]) << 0) | (cast(u16, p.[1]) << 8);
}

// Get unsigned 32-bit integer from first 4 bytes of a given bytes chunk.
// Function assumes little endian order of bytes inside the chunk.
fun unsafe_get_u32le(p: [*]u8) => u32 {
	ret (cast(u32, p.[0]) << 0) | (cast(u32, p.[1]) << 8) |
	       (cast(u32, p.[2]) << 16) | (cast(u32, p.[3]) << 24);
}

// Get unsigned 64-bit integer from first 8 bytes of a given bytes chunk.
// Function assumes little endian order of bytes inside the chunk.
fun unsafe_get_u64le(p: [*]u8) => u64 {
	ret (cast(u64, p.[0]) << 0) | (cast(u64, p.[1]) << 8) |
	       (cast(u64, p.[2]) << 16) | (cast(u64, p.[3]) << 24) |
	       (cast(u64, p.[4]) << 32) | (cast(u64, p.[5]) << 40) |
	       (cast(u64, p.[6]) << 48) | (cast(u64, p.[7]) << 56);
}

/*
Bytes chunk (alias to []u8)

Represents continuous region in memory in form of raw bytes.
Chunk by definition is nil when number of bytes in it equals zero.

Meaning of chunk contents are dependant on its specific usage. For example
bytes can represent a utf-8 string, bitmap of an image or a network packet.
Users should specify meaning of chunk in place where they use it.

Chunk can point to static, stack or heap allocated memory. It is
responsibility of the user to keep track of different usages and deallocate
backing memory.

Depending on particular usage chunk may either be:
  1. a view into memory owned by someone else
  2. owned memory region
*/
type bx struct {
    ptr: [*]u8,
    len: uint,
}

type str []u8

fun print(s: str)

fun panic(s: str) {
	panic_trap();
}

fun panic_stub() {
	panic_trap();
}

fun make_bx(ptr: [*]u8, len: uint) => []u8 {
    var c: []u8 = dirty;
	if len == 0 {
		c.ptr = nil;
		c.len = 0;
		ret c;
	}

    c.ptr = ptr;
    c.len = len;
    ret c;
}

// Copies n bytes of memory from src to dest. Number of copied bytes
// must be greater than zero.
//
// Do not use for overlapping memory regions. 
fun unsafe_copy(dest: [*]u8, src: [*]u8, n: uint) {
	for i in range(n) {
		dest.[i] = src.[i];
	}
}

// Same as unsafe_copy, but bytes are copied in reverse order.
fun unsafe_reverse_copy(dest: [*]u8, src: [*]u8, n: uint) {
	var i: uint = 0;
	var j: uint = n - 1;
	for i < n {
		dest.[j] = src.[i];
		i += 1;
		j -= 1;
	}
}

// Returns number of bytes copied.
fun copy(dest: []u8, src: []u8) => uint {
    let n: uint = min_uint(dest.len, src.len);
    if n == 0 {
        ret 0;
    }

    must(dest.ptr != nil);
    must(src.ptr != nil);
	must(dest.ptr != src.ptr);

    unsafe_copy(dest.ptr, src.ptr, n);
	ret n;
}

fun bx_slice_head(c: []u8, n: uint) => []u8 {
	must(n <= c.len);
	ret make_bx(c.ptr, n);
}

fun bx_slice_tail(c: []u8, n: uint) => []u8 {
	must(n <= c.len);
	ret make_bx(c.ptr + n, c.len - n);
}

/*
Find and return index of the first occurrence of byte {x} inside a bytes chunk {b}.

If view does not contain specified byte {x}, then fuction returns -1.
Otherwise return value is guaranteed to satisfy 0 <= i < len.
*/
fun bx_index_byte(c: []u8, x: u8) => sint {
	for i in range(c.len) {
		if c.ptr.[i] == x {
			ret tint(sint, i);
		}
	}
	ret -1;
}

/*
Returns true if two strings contain the same data.
*/
fun str_equal(a: str, b: str) => bool {
	if a.len != b.len {
		ret false;
	}
	if a.ptr == b.ptr {
		ret true;
	}

	for i in range(a.len) {
		if a.ptr.[i] != b.ptr.[i] {
			ret false;
		}
	}
	ret true;
}

fun make_str(ptr: [*]u8, len: uint) => str {
    ret make_bx(ptr, len);
}

fun unsafe_make_str_from_cstr_ptr(ptr: [*]u8) => str {
	must(ptr != nil);

	var i: uint = 0;
	for ptr.[i] != 0 {
		i += 1;
	}

	ret make_str(ptr, i);
}

/*
Strings chunk (alias to []str)
*/
type sx struct {
	ptr: [*]str,
    len: uint,
}

fun make_sx(ptr: [*]str, len: uint) => []str {
    var c: []str = dirty;
	if len == 0 {
		c.ptr = nil;
		c.len = 0;
		ret c;
	}

    c.ptr = ptr;
    c.len = len;
    ret c;
}

fun djb2_hash64(c: []u8) => u64 {
    var h: u64 = 5381; // magic number
    for i in range(c.len) {
        h = ((h << 5) + h) + tint(u64, c.ptr.[i]);
    }
    ret h;
}
